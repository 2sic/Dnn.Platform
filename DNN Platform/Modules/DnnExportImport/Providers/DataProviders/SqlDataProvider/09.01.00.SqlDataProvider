-- DNN-9447: Import/Export feature

IF NOT EXISTS ( SELECT 1 FROM {databaseOwner}{objectQualifier}Schedule
				WHERE TypeFullName = N'Dnn.ExportImport.Components.Scheduler.ExportImportScheduler, DotNetNuke.Modules.SiteExportImport')
BEGIN
	INSERT INTO {databaseOwner}{objectQualifier}Schedule
	(   [TypeFullName]
	  , [TimeLapse]
	  , [TimeLapseMeasurement]
	  , [RetryTimeLapse]
	  , [RetryTimeLapseMeasurement]
	  , [RetainHistoryNum]
	  , [AttachToEvent]
	  , [CatchUpEnabled]
	  , [Enabled]
	  , [ObjectDependencies]
	  , [Servers]
	  , [CreatedByUserID]
	  , [CreatedOnDate]
	  , [LastModifiedByUserID]
	  , [LastModifiedOnDate]
	  , [FriendlyName]
	) VALUES
    (   N'Dnn.ExportImport.Components.Scheduler.ExportImportScheduler, DotNetNuke.Modules.SiteExportImport'
	  , 5
	  , N'm'
	  , 1
	  , N'm'
	  , 100
	  , N''
	  , 0
	  , 1
	  , N''
	  , NULL
	  , NULL
	  , NULL
	  , NULL
	  , NULL
	  , N'Site Import/Export'
	 )
END
GO

IF OBJECT_ID(N'{databaseOwner}[{objectQualifier}ExportImportJobs]', N'U') IS NULL
	CREATE TABLE {databaseOwner}[{objectQualifier}ExportImportJobs](
		[JobId]       int IDENTITY(1,1) NOT NULL,
		[PortalId]    int NOT NULL,
		[JobType]     int NOT NULL,
		[JobStatus]   int NOT NULL DEFAULT 0,
		[CreatedBy]   int NOT NULL,
		[CreatedOn]   DateTime NOT NULL DEFAULT GetUtcDate(),
		[UpdatedOn]   DateTime NOT NULL DEFAULT GetUtcDate(),
		[CompletedOn] DateTime,  -- UTC date
		[ExportFile]  nvarchar(100),
		[JobObject]   nvarchar(max),

		CONSTRAINT [PK_{objectQualifier}ExportImportJobs] PRIMARY KEY ([JobId])
	)
GO

IF NOT EXISTS ( SELECT 1 FROM sys.indexes
				WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}ExportImportJobs]')
				AND name = N'IX_{objectQualifier}ExportImportJobs_JobType')
	CREATE NONCLUSTERED INDEX [IX_{objectQualifier}ExportImportJobs_JobType]
		ON {databaseOwner}[{objectQualifier}ExportImportJobs] ([JobType])
GO

IF NOT EXISTS ( SELECT 1 FROM sys.indexes
				WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}ExportImportJobs]')
				AND name = N'IX_{objectQualifier}ExportImportJobs_JobStatus')
	CREATE NONCLUSTERED INDEX [IX_{objectQualifier}ExportImportJobs_JobStatus]
		ON {databaseOwner}[{objectQualifier}ExportImportJobs] ([JobStatus])
GO

IF NOT EXISTS ( SELECT 1 FROM sys.indexes
				WHERE object_id = OBJECT_ID(N'{databaseOwner}[{objectQualifier}ExportImportJobs]')
				AND name = N'IX_{objectQualifier}ExportImportJobs_CreatedOn')
	CREATE NONCLUSTERED INDEX [IX_{objectQualifier}ExportImportJobs_CreatedOn]
		ON {databaseOwner}[{objectQualifier}ExportImportJobs] ([CreatedOn])
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImportJobs_Add]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_Add]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_Add]
	@PortalId int,
	@JobType int,
	@CreatedBy int,
	@ExportFile nvarchar(100),
	@JobObject nvarchar(max)
AS
BEGIN
	INSERT INTO
		{databaseOwner}[{objectQualifier}ExportImportJobs]
		(
			PortalId,
			JobType,
			CreatedBy,
			ExportFile,
			JobObject
		)
	VALUES
		(
			IsNull(@PortalId, -1),
			@JobType,
			IsNull(@CreatedBy, -1),
			@ExportFile,
			@JobObject
		)

	SELECT @@IDENTITY
END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImportJobs_UpdateStatus]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_UpdateStatus]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_UpdateStatus]
	@JobId int,
	@JobStatus int,
	@CompletedOn datetime = NULL
AS
BEGIN
	UPDATE
		{databaseOwner}[{objectQualifier}ExportImportJobs]
	SET
		JobStatus = @JobStatus,
		UpdatedOn = GetUtcDate(),
		CompletedOn = @CompletedOn
	WHERE
		JobId = @JobId
END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImportJobs_FirstActive]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_FirstActive]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_FirstActive]
AS
BEGIN
	SELECT
		TOP(1) *
	FROM
		{databaseOwner}[{objectQualifier}ExportImportJobs]
	WHERE
		JobStatus in (0, 1) -- 0=Submitted, 1=InProgress
	ORDER BY
		JobId ASC
END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImportJobs_GetAll]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_GetAll]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImportJobs_GetAll]
    @PortalID  int = -1, -- might be null or -1 for all portals
    @PageSize  int = 10, -- number of items per page
    @PageIndex int =  0  -- page number starting at 0
AS
BEGIN
	SET @PortalId = IsNull(@PortalId, -1)
	SET @PageSize = IsNull(@PortalId, 10)
	SET @PageIndex = IsNull(@PageIndex, 0)

	; WITH AllJobs AS (
		SELECT
			ROW_NUMBER() OVER (ORDER BY JobId ASC) AS RowNum, *
		FROM
			{databaseOwner}[{objectQualifier}ExportImportJobs]
		WHERE
			PortalID = @PortalID
	)
	SELECT
		TOP(@PageSize) *
	FROM
		AllJobs
	WHERE
		RowNum >= (@PageIndex * @PageSize + 1)
END
GO

-- Users Import/Export
IF object_id(N'{databaseOwner}[{objectQualifier}UserName]', 'FN') IS NOT NULL
    DROP FUNCTION {databaseOwner}[{objectQualifier}UserName]
GO

CREATE FUNCTION {databaseOwner}[{objectQualifier}UserName]
(
	@userId Int
)
RETURNS 
	nVarChar(255)
AS
	BEGIN
		DECLARE @Username AS nVarChar(255)

		SELECT  @Username = Username FROM {databaseOwner}[{objectQualifier}Users] WHERE UserID = @UserId
		RETURN  @Username
	END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}UserIdByUsername]', 'FN') IS NOT NULL
    DROP FUNCTION {databaseOwner}[{objectQualifier}UserIdByUsername]
GO

CREATE FUNCTION {databaseOwner}[{objectQualifier}UserIdByUsername]
(
	@username Int
)
RETURNS 
	int
AS
	BEGIN
		DECLARE @UserId AS int

		SELECT  @UserId = UserID FROM {databaseOwner}[{objectQualifier}Users] WHERE Username = @username
		RETURN  @UserId
	END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImport_GetAllUsers]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetAllUsers]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetAllUsers]
	@PortalID        int,
	@PageIndex       int,
	@PageSize        INT,
	@IncludeDeleted  bit
AS
BEGIN
	DECLARE 
		@PageLowerBound INT, 
		@PageUpperBound INT, 
		@RowsToReturn int

	EXEC {databaseOwner}[{objectQualifier}CalculatePagingInformation] @PageIndex, @PageSize, @RowsToReturn output, @PageLowerBound output, @PageUpperBound output
		 
	BEGIN
		WITH [tmpUsers] AS (
			SELECT U.*,{databaseOwner}[{objectQualifier}UserName](U.CreatedByUserId) CreatedByUserName,{databaseOwner}[{objectQualifier}UserName](U.LastModifiedByUserId) LastModifiedByUserName,  row_number() over (order by U.CreatedOnDate) AS RowId
			,  row_number() over (order by U.CreatedOnDate DESC) AS RowIdDesc
				FROM {databaseOwner}[{objectQualifier}Users] U INNER JOIN {databaseOwner}[{objectQualifier}vw_Users] VU ON U.UserID=VU.UserID
				WHERE VU.PortalID = @PortalID AND U.IsDeleted <= CASE @IncludeDeleted WHEN 0 THEN 0 ELSE 1 END
		)
		SELECT (RowIdDesc + RowId - 1) Total,  * FROM [tmpUsers]
			WHERE RowId > @PageLowerBound AND RowId < @PageUpperBound
			ORDER by RowId
	END
	SET ROWCOUNT 0;
END
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImport_GetUserPortal]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserPortal]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserPortal]
	@PortalId  int,
	@UserId    int
AS
SELECT 
	   [UserId]
      ,[PortalId]
      ,[UserPortalId]
      ,[CreatedDate]
      ,[Authorised]
      ,[IsDeleted]
      ,[RefreshRoles]
      ,[VanityUrl]
 	FROM {databaseOwner}[{objectQualifier}UserPortals]
	WHERE UserId = @UserId AND PortalId = @PortalId
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImport_GetAspNetUser]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetAspNetUser]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetAspNetUser]
@Username nvarchar(256)
AS
SELECT * 
 	FROM {databaseOwner}[aspnet_Users]
	WHERE  UserName=@Username
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImport_GetUserMembership]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserMembership]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserMembership]
@UserId uniqueidentifier,
@ApplicationId uniqueidentifier
AS
SELECT * 
 	FROM {databaseOwner}[aspnet_Membership]
	WHERE UserId=@UserId AND ApplicationId=@ApplicationId
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportImport_GetUserRoles]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserRoles]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportImport_GetUserRoles]
	@PortalId  int,
	@UserId    int
AS
	SELECT [UserRoleID]
      ,[RoleID]
      ,[UserID]
      ,[PortalID]
      ,[RoleName]
      ,[ExpiryDate]
	  ,[IsTrialUsed]
	  ,[EffectiveDate]
	  ,[CreatedByUserID]
      ,[CreatedOnDate]
      ,[LastModifiedByUserID]
      ,[LastModifiedOnDate]
	  ,[Status]
      ,[IsOwner]
	  ,{databaseOwner}[{objectQualifier}UserName](CreatedByUserId) CreatedByUserName
	  ,{databaseOwner}[{objectQualifier}UserName](LastModifiedByUserId) LastModifiedByUserName
	FROM {databaseOwner}[{objectQualifier}vw_UserRoles]
	WHERE UserID = @UserId AND PortalID = @PortalId
GO

--Taxonomy procedures
IF object_id(N'{databaseOwner}[{objectQualifier}ExportTaxonomy_ScopeTypes]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_ScopeTypes]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_ScopeTypes]
AS
	SELECT
		*
	FROM
		{databaseOwner}{objectQualifier}Taxonomy_ScopeTypes
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportTaxonomy_VocabularyTypes]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_VocabularyTypes]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_VocabularyTypes]
AS
	SELECT
		*
	FROM
		{databaseOwner}{objectQualifier}Taxonomy_VocabularyTypes
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportTaxonomy_Terms]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_Terms]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_Terms]
AS
	SELECT
		t.*, u1.Username AS CreatedByUserName, u2.Username AS LastModifiedByUserName
	FROM
		{databaseOwner}{objectQualifier}Taxonomy_Terms AS t
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users AS u1 on t.CreatedByUserID = u1.UserID
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users AS u2 on t.LastModifiedByUserID = u2.UserID
GO

IF object_id(N'{databaseOwner}[{objectQualifier}ExportTaxonomy_Vocabularies]', 'P') IS NOT NULL
    DROP PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_Vocabularies]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}ExportTaxonomy_Vocabularies]
AS
	SELECT
		v.*, u1.Username AS CreatedByUserName, u2.Username AS LastModifiedByUserName
	FROM
		{databaseOwner}{objectQualifier}Taxonomy_Vocabularies AS v
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users AS u1 on v.CreatedByUserID = u1.UserID
			LEFT OUTER JOIN {databaseOwner}{objectQualifier}Users AS u2 on v.LastModifiedByUserID = u2.UserID
GO
